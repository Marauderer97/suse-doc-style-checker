<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:include href="library.xsl"/>

  <xsl:template match="*" mode="part-title">Ulinks</xsl:template>


  <xsl:template match="ulink">
    <xsl:variable name="url" select="normalize-space(@url)"/>

    <!-- Tanja questioned if this was helping much, we could just as well
         change the appropriate stylesheet to do the right thing. -->
    <!-- <xsl:if test="*|text()">
      <result>
        <error>Ulink <xsl:call-template name="createid"/> includes text.</error>
        <suggestion>Remove all text from inside <tag>ulink</tag>. This allows for printing the URL.</suggestion>
      </result>
    </xsl:if> -->

    <!-- Probably non-exhaustive. -->
    <xsl:if test="not(starts-with($url, 'http:') or
                      starts-with($url, 'https:') or
                      starts-with($url, 'ftp:') or
                      starts-with($url, 'gopher:') or
                      starts-with($url, 'nntp:') or
                      starts-with($url, 'rtsp:') or
                      starts-with($url, 'mms:'))">
      <xsl:choose>
        <xsl:when test="starts-with($url, 'file:') or
                        starts-with($url, 'info:') or
                        starts-with($url, 'smb:')">
          <result>
            <error>Ulink
              <xsl:call-template name="createid">
                <xsl:with-param name="use-url-attribute" select="1"/>
              </xsl:call-template>
              links to a local file, directory or network.
            </error>
            <suggestion>To reference local files, directories, or networks,
              use the <tag>filename</tag> element.
            </suggestion>
          </result>
        </xsl:when>
        <xsl:when test="starts-with($url, 'mailto:') or contains($url, '@')">
          <result>
            <error>Ulink
              <xsl:call-template name="createid">
                <xsl:with-param name="use-url-attribute" select="1"/>
              </xsl:call-template>
              links to an e-mail address.
            </error>
            <suggestion>To reference e-mail addresses, use the
              <tag>email</tag> element.
            </suggestion>
          </result>
        </xsl:when>
        <xsl:when test="contains($url, '.com') or contains($url, '.net') or
                        contains($url, '.org') or contains($url, '.biz') or
                        contains($url, '.co.') or contains($url, '.gov') or
                        contains($url, '.edu') or contains($url, '.de') or
                        contains($url, '.fr') or contains($url, '.es') or
                        contains($url, '.ru') or contains($url, '.br') or
                        contains($url, '.cn') or contains($url, '.in')">
          <result>
            <error>Ulink
              <xsl:call-template name="createid">
                <xsl:with-param name="use-url-attribute" select="1"/>
              </xsl:call-template>
              appears to contain no a protocol identifier.
            </error>
            <suggestion>If you have forgotten an <quote>http://</quote>
              or similar prefix, add it.
            </suggestion>
          </result>
        </xsl:when>
        <xsl:otherwise>
          <result>
            <error>Ulink
              <xsl:call-template name="createid">
                <xsl:with-param name="use-url-attribute" select="1"/>
              </xsl:call-template>
              appears to contain no a protocol identifier.
            </error>
            <suggestion>If you have forgotten an <quote>http://</quote>
              or similar prefix, add it.
            </suggestion>
            <suggestion>To reference local files, directories,
              or networks, use the <tag>filename</tag> element.
            </suggestion>
            <suggestion>To reference e-mail addresses, use the
              <tag>email</tag> element.
            </suggestion>
          </result>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
