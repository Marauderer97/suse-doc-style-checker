<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:include href="library.xsl"/>

  <xsl:template match="*" mode="part-title">Admonitions</xsl:template>


  <xsl:template match="caution|warning|important|note|tip">
    <xsl:if test="self::caution">
      <result>
        <xsl:call-template name="sourcehint"/>
        <error>Admonition <xsl:call-template name="createid"/>
          is a <tag>caution</tag> element.
        </error>
        <suggestion>Use the <tag>warning</tag> element instead.
        </suggestion>
      </result>
    </xsl:if>

    <xsl:if test="not(title)">
      <xsl:variable name="output-tag-type">
        <xsl:choose>
          <xsl:when
            test="self::caution|self::warning|self::important">error</xsl:when>
          <xsl:otherwise>warning</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>

      <result>
        <xsl:call-template name="sourcehint"/>
        <xsl:element name="{$output-tag-type}">
          <tag><xsl:value-of select="local-name(.)"/></tag> element
          <xsl:call-template name="createid"/>
          does not contain a <tag>title</tag> element.
        </xsl:element>
        <suggestion>Add a <tag>title</tag> element.
        </suggestion>
      </result>
    </xsl:if>
  </xsl:template>


</xsl:stylesheet>
