<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.gitorious.org/style-checker/style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:template match="*" mode="part-title">Admonitions</xsl:template>


  <xsl:template match="caution|warning|important|note|tip">
    <xsl:if test="self::caution">
      <result type="error">
        <xsl:call-template name="sourcehint"/>
        <message>Admonition <xsl:call-template name="createid"/>
          is a <tag>caution</tag> element.
        </message>
        <suggestion>Use the <tag>warning</tag> element instead.
        </suggestion>
      </result>
    </xsl:if>

    <xsl:if test="not(title)">
      <xsl:variable name="output-type">
        <xsl:choose>
          <xsl:when
            test="self::caution|self::warning|self::important">error</xsl:when>
          <xsl:otherwise>warning</xsl:otherwise>
        </xsl:choose>
      </xsl:variable>

      <result type="{$output-type}">
        <xsl:call-template name="sourcehint"/>
        <message>
          <tag><xsl:value-of select="local-name(.)"/></tag> element
          <xsl:call-template name="createid"/>
          does not contain a <tag>title</tag> element.
        </message>
        <suggestion>Add a <tag>title</tag> element.
        </suggestion>
      </result>
    </xsl:if>
  </xsl:template>


</xsl:stylesheet>
